plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion COMPILE_SDK
    buildToolsVersion BUILD_TOOL_VERSION

    defaultConfig {
        applicationId "com.wtb.robot"
        minSdkVersion MIN_SDK
        targetSdkVersion TARGET_SDK
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation rootProject.ext.dependencies['kotlin']
    implementation rootProject.ext.dependencies['core-ktx']
    implementation rootProject.ext.dependencies['appcompat']
    implementation rootProject.ext.dependencies['constraintlayout']
    implementation rootProject.ext.dependencies['material']
    implementation rootProject.ext.dependencies['xposed']
    implementation rootProject.ext.dependencies['xposed_source']
    implementation project(path: ':hook_lib')
}

// 每次修改运行后自动让 VXP 中的模块`即时生效` ,需要将 (Debug Configurations) - Before Launch - Gradle aware Make - 修改为 :app:installDebug
afterEvaluate {
    installDebug.doLast {
        updateVirtualXposedAPP.execute()
        rebootVirtualXposedAPP.execute()
    }
}

// 更新 VXP 中的 app
task updateVirtualXposedAPP(type: Exec) {
    def pkg = android.defaultConfig.applicationId
    commandLine android.adbExecutable, 'shell', 'am', 'broadcast', '-a', 'io.va.exposed.CMD', '-e', 'cmd', 'update', '-e', 'pkg', pkg
}
// 重启 VXP
task rebootVirtualXposedAPP(type: Exec) {
    commandLine android.adbExecutable, 'shell', 'am', 'broadcast', '-a', 'io.va.exposed.CMD', '-e', 'cmd', 'reboot'
}